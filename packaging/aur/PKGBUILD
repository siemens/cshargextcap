# Maintainer: Harald Albrecht <harald.albrecht@siemens.com>

# - https://wiki.archlinux.org/title/creating_packages
# - https://wiki.archlinux.org/title/Go_package_guidelines
# - https://wiki.archlinux.org/title/PKGBUILD
#
# Wireshark:
# - https://gitlab.archlinux.org/archlinux/packaging/packages/wireshark/-/blob/main/PKGBUILD?ref_type=heads

pkgbase='cshargextcap'
pkgname=('cshargextcap-cli' 'cshargextcap-desktop')
pkgver='0.9.2'
pkgrel=1
pkgdesc='Wireshark extcap plugin for container traffic capture live streaming'
url='https://github.com/siemens/cshargextcap'
arch=('x86_64')
license=('MIT')
makedepends=('go')
source=("https://github.com/siemens/cshargextcap/archive/refs/tags/v${pkgver}.tar.gz")

prepare() {
    cd "${pkgbase}-${pkgver}"
    mkdir -p build
}

build() {
    cd "${pkgbase}-${pkgver}"
    go build \
        -tags netgo,osusergo \
        -trimpath \
        -buildmode pie \
        -mod=readonly \
        -modcacherw \
        -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
        -o build/ \
        ./cmd/cshargextcap
}

package_cshargextcap-cli() {
    depends=('wireshark-cli')
    
    cd "${pkgbase}-${pkgver}"
    install -Dm 755 build/cshargextcap -t "${pkgdir}"/usr/lib/wireshark/extcap    
}

package_cshargextcap-desktop() {
    pkgdesk+=" - desktop integration"
    depends=('cshargextcap-cli' 'wireshark-qt')

    cd "${pkgbase}-${pkgver}"
    install -Dm 644 packaging/linux/com.github.siemens.packetflix.desktop -t "${pkgdir}"/usr/share/applications
}
